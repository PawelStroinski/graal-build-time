{:paths ["."]
 :tasks
 {:requires ([babashka.fs :as fs]
             [babashka.tasks :as tasks]
             [babashka.process :refer [tokenize]]
             [build-shared :as bs]
             [clojure.string :as str])
  :init
  (do
    (def windows? (str/starts-with? (System/getProperty "os.name")
                                    "Windows"))
    (defn clojure
      "Clojure defaulting to stderr output"
      [arg & args]
      (apply tasks/clojure (str "-J-Dclojure.main.report=stderr " arg) args)))

  clean {:doc "Clean target dir"
         :task (bs/clean {})}

  compile-sources {:doc "Compile sources"
                   :task (when (bs/needs-compile?)
                           (clojure "-T:build compile-sources"))}

  jar {:doc "Build jar"
       :task (when (bs/needs-jar?)
               (clojure "-T:build jar"))}

  install {:doc "Install jar in local maven repo"
           :task (clojure "-T:build install")}

  uber {:doc "Build uberjar for testing"
        :task (when (seq (fs/modified-since bs/uberjar ["deps.edn" "src" "test"]))
                (clojure "-T:build uber"))}

  graalvm {:doc "Checks GRAALVM_HOME env var"
           :task
           (let [env (System/getenv "GRAALVM_HOME")]
             (assert env "Set GRAALVM_HOME")
             env)}

  native-image
  {:doc     "Builds native image"
   :depends [graalvm uber]
   :task    (when (seq (fs/modified-since "native-test"
                                          bs/uberjar))
              (shell (str (fs/file graalvm
                                   "bin"
                                   (if windows?
                                     "gu.cmd"
                                     "gu")))
                     "install" "native-image")
              (shell (str (fs/file graalvm
                                   "bin"
                                   (if windows?
                                     "native-image.cmd"
                                     "native-image")))
                     ;; note: we are omitting --initialize-at-build-time
                     "-jar" "target/test.jar"
                     "--no-fallback"
                     "--no-server"
                     "-H:Name=native-test"))}

  native-image-test
  {:doc "Run integration test"
   :depends [native-image]
   :task    (shell "./native-test")}

  tag (do
        (shell (str "git commit --allow-empty -m " (str "v"(deref bs/version))))
        (shell (str "git tag v" (deref bs/version)))
        (shell "git push --atomic origin main"
               (str "v" (deref bs/version))))

  current-tag (->> (shell {:out :string
                           :continue true} "git describe")
                   :out
                   str/trim
                   (re-matches (re-pattern "^v\\d+\\.\\d+\\.\\d+$")))

  current-branch (->> (shell {:out :string} "git rev-parse --abbrev-ref HEAD")
                      :out
                      str/trim)

  should-release {:depends [current-tag current-branch]
                  :task (and current-tag (= "main" current-branch))}

  deploy {:doc "Deploys to clojars"
          :depends [should-release]
          :task (when should-release
                  (clojure "-T:build deploy"))}

  ,}
 ,}
